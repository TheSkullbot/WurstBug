package Founder

// STDLib
import Assets
import UnitObjEditing
import LinkedList
import ObjectIds

// Local imports
import LocalObjectIDs
import SkullUnit
import SkullHeroDefinition

//=============================================================
// Unit definition
//=============================================================

class FounderDefinition extends SkullHeroDefinition

    construct( int unitId )
        super( unitId, UnitIds.paladin )
        train     = "Revive your"
        name      = "Founder"
        hotkey    = "F"
        desc      = "Your avatar in this wild world"
        abilities = commaList( ABILITY_REPAIR, ABILITY_HARVEST, ABILITY_INVENTORY_6 )
        built     = commaList( BUILDING_TENT, BUILDING_CHICKENCOOP, BUILDING_CAMPFIRE, BUILDING_FENCE, BUILDING_WELL )
        features  = asList( "Can be revived at Farm House or Altar" )
        this.setDefault()
        this.setModelFile( Units.highElfPeasant1 )
        this.setIconGameInterface( Icons.bTNElfVillager )
        this.setSpeedBase(300)
        this.setSpeedMinimum(300)
        this.setSpeedMaximum(300)
        this.setScalingValue(1.10)
        this.setCollisionSize(12)
        this.setHitPointsMaximumBase(300)
        this.setHitPointsRegenerationType("")
        this.setManaMaximum(100)
        this.setManaInitialAmount(100)
        this.setSightRadiusDay(800)
        this.setSightRadiusNight(400)
        this.setPrimaryAttribute( "AGI" )
        this.setStrengthPerLevel(2)
        this.setAgilityPerLevel(3)
        this.setIntelligencePerLevel(2)
        this.setStartingStrength(10)
        this.setStartingAgility(10)
        this.setStartingIntelligence(10)
        this.setAttack1DamageBase(666)
        this.setAttack1CooldownTime(1.0)
        this.setBuildTime(5)
        this.setGoldCost(50)

@compiletime function generate()
    new FounderDefinition( UNIT_FOUNDER )            

//=============================================================
// Chicken class
//=============================================================

public class Founder extends SkullUnit

    static int limit = 1

    //=============================================================
    // Constructor
    //=============================================================

    construct( unit unitID )
        super( unitID )

    static function initialize()
        Founder.register( UNIT_FOUNDER, unitID -> new Founder( unitID ) )


//=============================================================
// Initialisation
//=============================================================

init
    Founder.initialize()
    for playerIndex = 0 to ( PLAYER_NEUTRAL_AGGRESSIVE - 1 )
        let whichPlayer = playerFromIndex(playerIndex)
        if( whichPlayer.getSlotState() == PLAYER_SLOT_STATE_PLAYING and not IsPlayerObserver( whichPlayer ) )			
            createUnit( whichPlayer, UNIT_FOUNDER, whichPlayer.getStartLocation(), (270.0).asAngleDegrees() )
            SetPlayerTechMaxAllowed( whichPlayer, UNIT_FOUNDER, Founder.limit )
            